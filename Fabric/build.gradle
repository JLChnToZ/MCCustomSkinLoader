apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
apply from: rootProject.file("BuildScripts/version.gradle")

version = getCSLVersion()
group = "customskinloader"
archivesBaseName = "CustomSkinLoader_Fabric"
ext.shortVersion = getShortVersion()

sourceCompatibility = targetCompatibility = 1.6
compileJava {
    sourceCompatibility = targetCompatibility = 1.6
}

minecraft {
    version = config.forge_mc_version + "-" + config.forge_version
    runDir = "run"
    mappings = config.mappings_version

    replace '@MOD_VERSION@', rootProject.ext.config.version
    replace '@MOD_FULL_VERSION@', project.version
    replaceIn 'CustomSkinLoader.java'
}

jar {
    exclude 'net/minecraft/client/renderer/texture/**'
}

repositories {
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    implementation 'org.spongepowered:mixin:0.8-SNAPSHOT'
}

mixin {
    add sourceSets.main, "mixins.customskinloader.refmap.json"
    reobfSrgFile = config.srg_notch_map
    reobfNotchSrgFile = config.srg_notch_map
}

sourceSets {
    main {
        java {
            //Common
            srcDir '../Common/source'
            //Fabric
            srcDir 'source'
        }
        resources {
            //Common
            srcDir '../Common/resource'
            exclude 'Profile.json'
            //Fabric
            srcDir 'resource'
        }
    }
}

processResources {
    expand([
            modVersion         : rootProject.ext.config.version,
            modFullVersion     : project.version,
            modShortFullVersion: shortVersion,
            mcVersion          : config.minecraft_version,
            mcFullVersions     : config.minecraft_full_versions,
            forgeVersion       : config.forge_version,
            gitVersion         : System.getenv("CIRCLE_SHA1"),
            buildUrl           : System.getenv("CIRCLE_BUILD_URL")
    ])
}

apply from: rootProject.file("BuildScripts/remap.gradle")
task afterBuild {
    doLast {
        //renameSrgJar
        def file0 = file("build/libs/${archivesBaseName}-${version}.jar")
        def file1 = file("build/libs/${archivesBaseName}-${version}-srg.jar")
        file0.renameTo(file1)

        //reobfSrgToNotch
        reobfSrgToNotch()

        //copyBuildFile
        copy {
            from 'build/libs'
            into '../build/libs'
            include '**/*.jar'
            exclude '**/*-dev.jar'
            exclude '**/*-srg.jar'
            exclude '**/*-sources.jar'
        }
    }
}
build.finalizedBy afterBuild